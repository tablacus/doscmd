;
;	Tablacus DOS cmd
;
_CMD:
	DI
	CALL	FRMEVL
	PUSH	HL
	LD	(BASSP),SP
	LD	HL,-64
	ADD	HL,SP
	LD	SP,HL

	CALL	FRESTR
	LD	B,0
	LD	C,(HL)
	INC	HL
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	LD	DE,BUFSTR
	LD	A,C
	OR	A
	JP	Z,WBOOT1
	LDIR
	XOR	A
	LD	(DE),A

	LD	A,(RAMAD0)
	CALL	ENASLT_00H	;ページ0をRAMに切り替える
	LD	A,(RAMAD1)
	LD	H,040H
	CALL	_ENASLT		;ページ1をRAMに切り替える

	LD	HL,FDRV
	LD	B,37
	XOR	A
ZERO_FCB:
	LD	(HL),A
	INC	HL
	DJNZ	ZERO_FCB

	LD	DE,BUFSTR
	CALL	FILE
	CALL	READ_DOS1
	OR	A
	JR	NZ,WBOOT1
SET_PARAM:
	LD	HL,FCB1
ZERO1:
	LD	(HL),H
	INC	L
	JR	NZ,ZERO1

	LD	B,0
	LD	HL,DTA1+1
PARAM1:
	LD	A,(DE)
	LD	(HL),A
	OR	A
	JR	Z,PARAM2
	INC	DE
	INC	HL
	INC	B
	JR	PARAM1
PARAM2:
	LD	A,B
	LD	(DTA1),A

	LD	DE,DTA1+1
	CALL	FILE
	PUSH	DE
	LD	HL,FDRV
	LD	DE,FCB1
	LD	BC,00010H
	LDIR
	POP	DE
	LD	HL,FCB2
	CALL	FILE
	LD	HL,FDRV
	LD	DE,FCB2
	LD	BC,00010H
	LDIR

PARAM_SWC:
	XOR	A
	LD	L,A
	LD	H,A
	LD	(TERMCODE),A
	LD	(DEFAB),HL
	CALL	00100H
WBOOT1:
	LD	SP,(BASSP)
	LD	A,(EXPTBL)	;メインROMスロット
	CALL	ENASLT_00H	;ページ0をROMに切り替える
	LD	A,(EXPTBL)	;メインROMスロット
	LD	H,040H
	CALL	_ENASLT		;ページ1をROMに切り替える
	XOR	A
	LD	(CSRSW),A
	POP	HL
	EI
	RET

SS1:
	INC	DE
SPCUT:				;スペースをカット
	LD	A,(DE)
	CP	020H
	JR	Z,SS1
	RET

TOZERO:
	PUSH	DE
TZ1:
	LD	A,(DE)
	CP	20H
	JR	C,TZ2
	INC	DE
	JR	TZ1
TZ2:
	XOR	A
	LD	(DE),A
	POP	DE
	RET

CAP:
	CP	'a'
	RET	C
	CP	'z'+1
	RET	NC
	SUB	020H
	RET
CAP2:
	CALL	CAP
CAP3:
	CALL	CAP4
	CP	021H
	RET	NC
	LD	A,020H
	RET
CAP4:
	CP	5
	RET	NZ
	LD	A,0E5H
	RET
FILE:
	XOR	A
	LD	(FDRV),A
	LD	H,A
	LD	L,A
	LD	(FDRV+14),HL
	CALL	SPCUT
	CALL	CCHK3
	JR	Z,DEVI1
	INC	DE
	LD	A,(DE)
	DEC	DE
	CP	':'
	JR	NZ,DEVI1
	LD	A,(DE)		;DRIVE
	CALL	CAP
	SUB	'@'
	INC	DE
	INC	DE
	LD	(FDRV),A
DEVI1:
	LD	A,(DE)
	SUB	05CH		;\
	JR	NZ,NOROOT
	LD	L,A		;A=0でH=0のはずなのでHL=0
	LD	(FDRV+26),HL
	INC	L
	LD	(FDRV+14),HL
	INC	DE
NOROOT:

SETDIR:
	CALL	FILED
	LD	A,(DE)
	CP	05CH		;\
	RET	NZ
	INC	DE
SETDIR1:
	RET

FILED:
	CALL	FILEI

	LD	B,8
FILE2:
	CALL	CCHKF
	RET	Z
	CP	'*'
	JR	Z,FILE9
	CP	'.'
	JR	Z,FILE4
	LD	(HL),A
	INC	HL
	DJNZ	FILE2

FILE3:
	CALL	CCHKF
	RET	Z
	CP	'.'
	JR	NZ,FILE3

FILE4:
	LD	HL,FNAME+8
	LD	B,3

FILE4L:
	CALL	CCHKF
	RET	Z
	CP	'.'
	JR	NZ,FILE12
	LD	(FNAME),A	;Parent directory(..)
	LD	(FNAME+1),A
	LD	A,020H
FILE12:
	CP	'*'
	JR	Z,FILE10
	LD	(HL),A
	INC	HL
	DJNZ	FILE4L
	RET

FILE9:				;名前の*処理、名前の残りを?で埋める
	CALL	FILE10
	JR	FILE3

FILE10:
	LD	A,'?'

FILL_MEMORY:				;HLからBバイトAで埋める
	LD	(HL),A
	INC	HL
	DJNZ	FILL_MEMORY
	RET

FILEI:				;名前＋拡張子をスペースで初期化
	LD	A,020H
	LD	BC,11*256
	LD	HL,FNAME
	PUSH	HL
	CALL	FILL_MEMORY
	POP	HL
	RET
FILE_E:

READ_DOS1:
	PUSH	DE
	LD	A,'C'
	LD	(FNAME+8),A
	LD	HL,"MO"
	LD	(FNAME+9),HL
	LD	DE,FDRV
	LD	C,00FH		;(BDOS)ファイルのオープン(_FOPEN)
	CALL	BDOS
	INC	A
	POP	DE
	JP	Z,WBOOT1
	PUSH	DE

	LD	HL,1
	LD	(FDRV+14),HL
	LD	L,H
	LD	(FDRV+33),HL
	LD	(FDRV+35),HL

	LD	DE,00100H
	LD	C,01AH		;(BDOS)DTAの設定
	CALL	BDOS

	LD	HL,BDOS0-00100H
	LD	DE,FDRV
	LD	C,027H		;(BDOS)ランダム・ブロック・リード
	CALL	BDOS
	DEC	A
	POP	DE
	RET

CCHKF:
	LD	A,(DE)
	INC	DE
CCHK2:
	INC	C
	DEC	C
	RET	NZ
CCHK3:				;ZF=1 で使えない文字
	CP	'+'
	RET	Z
	CP	','
	RET	Z
	CP	'/'
	RET	Z
	CP	':'
	RET	Z
	CP	';'
	RET	Z
	CP	'='
	RET	Z
	CP	05CH	;\
	RET	Z
	CP	020H
	RET	NC
	CP	A		;Z=1
	RET
READ_DOS1_E:

CALSLT_BIOS:
	LD	IY,(EXPTBL-1) ;メインROMスロット
	JP	_CALSLT

INT38H_ROM:
	LD	A,(EXPTBL)	;メインROMに切り替える
	CALL	ENASLT_00H
	CALL	00038H
	LD	A,(RAMAD0)	;メインRAMに切り替える
;	JP	ENASLT_00H
;
;ページ0専門のENASLT
;in
;A←スロット
;破壊
;ABCHL
;
; ページ2 に配置
ENASLT_00H:
	DI
	LD	H,A
	AND	3
	LD	C,A
	IN	A,(0A8H)
	LD	B,0FCH	;ページ0
	AND	B
	OR	C
	BIT	7,H
	JP	Z,ENASLT_NOEXT
				;拡張スロットの処理
	PUSH	DE
	CALL	ENATBL
	RRCA
	RRCA
	LD	C,A
	LD	A,E
	CALL	AT_3B
	LD	(HL),E
	POP	DE
	RET
;
;	ENASLOTの補助（ページ0・003BH～にも配置）
;
AT_3B:				;ENASUB 対象の拡張スロットを切り替えるために基本スロットを切り替える
	OUT	(0A8H),A
	LD	A,(0FFFFH)	;拡張スロットの切り替え
	CPL
	AND	B
	OR	C
	LD	(0FFFFH),A
	LD	E,A
				;基本スロットの切り替え
	LD	A,D
	OUT	(0A8H),A
	RET
AT_3B_E:

	DS	0BFFFH-$
TERMCODE:
	DB	0

RD1SIZE	EQU	READ_DOS1_E-READ_DOS1
RD2SIZE	EQU	READ_DOS2_E-READ_DOS2

F1SIZE	EQU	FILE_E-FILE
F2SIZE	EQU	FILE_DOS2_E-FILE_DOS2

