;
;	Tablacus DOS cmd
;
TERM:
	LD	A,B
	LD	(TERMCODE),A
	LD	HL,0
	PUSH	HL
	LD	HL,(DEFAB)
	JP	(HL)
BDOS1:
	INC	C
	DEC	C
				;BDOS0+013Hを0にするとバッチ停止されるため0が書き込みされる場合がある
	JP	Z,0		;だから、ここのアドレスをBDOS0+011HもしくはBDOS0+012Hにする
	LD	(SAVE_SP),SP
	LD	(SAVE_A),A
	LD	(SAVE_IX),IX
	LD	(SAVE_IY),IY
	LD	A,C
	CP	062H		;_TERM エラーコードを返して終了
	JR	Z,TERM
	LD	A,(SAVE_SP+1)
	ADD	A,A
	JR	C,BDOS_SPOK
	LD	SP,(BASSP)
BDOS_SPOK:
	LD	A,C
	CP	9		;_STROUT 文字列出力
	JP	Z,_SYS09
	CP	063H		;_DEFAB アボート終了ルーチンの定義
	JR	NZ,BDOS2
	LD	(DEFAB),DE
BDOS2:
	CP	01AH		;_SETDTA DTAの設定
	JR	NZ,BDOS2_
	LD	(_DTA),DE
BDOS2_:
	LD	(SAVE_C),A
	CP	00FH		;_FOPEN ファイルオープン
	JR	C,SAVEFCB0
	CP	012H		;_SNEXT ファイル検索 続き
	JR	NZ,NOBD12
	LD	DE,(FCB11)
NOBD12:
	CP	018H		;_LOGIN ログインベクトルの獲得
	JR	C,SAVEFCB36
	CP	021H		;_RDRND ランダム読み出し
	JR	C,SAVEFCB0
	CP	028H+1		;_WRZER ゼロ書き込みを伴うランダム書き込み+1
	JR	C,SAVEFCB36
	CP	031H		;_DPARM ディスクパラメータの獲得
	JR	Z,SAVEFCB36
	CP	040H		;_FFIRST 最初のエントリ検索
	JR	Z,SAVE_PATH
	CP	042H		;_FNEW 新しいエントリの検索
	JR	Z,SAVE_PATH
	CP	043H		;_OPEN ファイルハンドルのオープン
	JR	Z,SAVE_PATH
	CP	044H		;_CREATE ファイルハンドルの作成
	JR	Z,SAVE_PATH
	CP	04CH 		;_HTEST ファイルハンドルの検査
	JR	Z,SAVE_PATH
	CP	04DH		;_DELETE ファイル、サブディレクトリの削除
	JR	C,SAVEFCB0
	CP	051H+1		;_FTIME ファイルの日付および時刻の獲得・設定
	JR	C,SAVE_PATH
	CP	059H		;_GETCD カレントディレクトリの獲得
	JR	C,SAVEFCB0
	CP	05CH+1		;_PFILE ファイル名の解析+1
	JR	C,SAVE_PATH
	CP	05EH		;_WPATH パス文字列全体の獲得
	JR	Z,SAVE_PATH
	CP	066H		;_EXPLAIN エラーコードの説明
	JR	Z,SAVE_PATH
	JR	SAVEFCB0
SAVEFCB36:
SAVE_PATH:
	LD	A,D
	CP	03FH		;ページ1にかぶる？
	JR	C,SAVEFCB0
	CP	080H
	JR	NC,SAVEFCB0
	LD	A,36
	BIT	6,C		;DOS2のファンクションコール？
	JR	Z,SAVEFCB1
	LD	A,64		;DOS2CALLは64バイト設定する
	JR	SAVEFCB1
SAVEFCB0:
	XOR	A
SAVEFCB1:
	LD	(DE_MEM_SIZE),A
	OR	A
	JR	Z,BDOS3
	PUSH	BC
	PUSH	HL
	LD	(SAVE_DE),DE
	LD	C,A
	LD	B,0
	LD	HL,ALT_MEM
	EX	DE,HL
	PUSH	DE
	LDIR
	XOR	A
	LD	(DE),A
	POP	DE
	POP	HL
	POP	BC
BDOS3:
	XOR	A
	LD	(IS_SAVE_IX_VAUE),A
	LD	A,C
	CP	040H		;最初のエントリ検索
	JR	C,BDOS3IX	;次のエントリ検索
	CP	042H+1		;新しいエントリの検索
	JR	NC,BDOS3IX
	LD	A,IXH
	CP	03FH		;ページ1にかぶる？
	JR	C,BDOS3IX
	CP	080H		;
	JR	NC,BDOS3IX
	LD	(IS_SAVE_IX_VAUE),A
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	POP	HL
	LD	DE,ALT_MEM2
	LD	BC,64
	PUSH	DE
	LDIR
	POP	IX
	POP	HL
	POP	DE
	POP	BC
BDOS3IX:
	XOR	A
	LD	(SAVE_HL+1),A
	LD	A,C
	CP	4EH		;_RENAME ファイルあるいはサブディレクトリ名の変更
	JR	Z,SAVE_HL1
	CP	4FH		;_MOVE ファイルあるいはサブディレクトリの移動
	JR	Z,SAVE_HL1
	CP	53H		;_HRENAME ファイルハンドルの名前の変更
	JR	Z,SAVE_HL1
	CP	54H		;_HMOVE ファイルハンドルの移動
	JR	Z,SAVE_HL1
	CP	05CH		;_PFILE ファイル名の解析
	JR	Z,SAVE_HL1
	CP	040H		;_FFIRST 最初のエントリ検索
	JR	Z,FIBCHECK
	CP	042H		;_FNEW 新しいエントリ検索
	JR	NZ,BDOS3HL
FIBCHECK:
	LD	A,(DE)
	INC	A		;DEがFIBならば0FFH
	JR	NZ,BDOS3HL
SAVE_HL1:
	LD	A,H
	CP	03FH		;ページ1にかぶる？
	JR	C,BDOS3HL
	CP	080H		;
	JR	NC,BDOS3HL
	LD	(SAVE_HL),HL
	PUSH	BC
	PUSH	DE
	LD	BC,64
	LD	DE,ALT_MEM3
	LDIR
	POP	DE
	POP	BC
	LD	HL,ALT_MEM3
BDOS3HL:
	LD	A,C
	CP	02FH
	JP	Z,RDABS
	CP	030H
	JP	Z,WRABS
BDOS_EXEC:
	LD	A,(SAVE_A)
	CALL	BDOS

	PUSH	AF
	LD	A,(SAVE_C)
	CP	01BH			;アロケーション情報の獲得
	JR	Z,BDOS4IX
	CP	06FH			;MSX-DOSのバージョン番号の獲得
	JR	Z,BDOS4IX
	LD	IX,(SAVE_IX)
	LD	IY,(SAVE_IY)
	LD	A,(IS_SAVE_IX_VAUE)		;最初のエントリ検索 + 次のエントリ検索
	OR	A
	JR	Z,BDOS4IX
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	POP	DE
	LD	HL,ALT_MEM2
	LD	BC,64
	LDIR
	POP	HL
	POP	DE
	POP	BC
BDOS4IX:
	LD	A,(DE_MEM_SIZE)
	OR	A
	JR	Z,BDOS4
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	C,A
	LD	B,0
	LD	HL,ALT_MEM
	LD	DE,(SAVE_DE)
	LDIR
	POP	HL
	POP	DE
	POP	BC
BDOS4:
	LD	A,(SAVE_HL+1)
	OR	A
	JR	Z,BDOS5
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	BC,11
	LD	HL,ALT_MEM3
	LD	DE,(SAVE_HL)
	LDIR
	POP	HL
	POP	DE
	POP	BC
BDOS5:
	LD	A,(SAVE_C)
	CP	06FH		;_DOSVER MSX-DOSのバージョン番号の獲得
	JR	NZ,BDOS7
	INC	B
	DEC	B
	JR	Z,S6F_1
	LD	E,C		;DOS2の場合
	LD	D,B		;MSXDOS2.SYSのバージョンをカーネルに合わす
S6F_1:
	DEC	IXH		;NEXTOR判別
	JR	Z,NEXTOR
	LD	IX,0ECEDH	;似非DOS判別用(INC IXがあるのでIXが0EDEDHになる)
	LD	HL,VER_BCD	;NEXTORじゃない場合はHLに似非DOSのバージョンが返る
NEXTOR:
	INC	IXH
BDOS7:
	CP	05BH		;_PARSE パス名の解析
	JR	Z,BDOS8
	CP	05EH		;_WPATH パス文字列全体の獲得
	JR	NZ,BDOS6
BDOS8:
	LD	A,(DE_MEM_SIZE)
	JR	Z,BDOS6
	PUSH	BC
	LD	BC,ALT_MEM
	SBC	HL,BC		;CF=0のハズ
	LD	BC,(SAVE_DE)
	ADD	HL,BC
	EX	DE,HL
	LD	BC,ALT_MEM
	AND	A		;念のためにCF=0にする
	SBC	HL,BC
	LD	BC,(SAVE_DE)
	ADD	HL,BC
	EX	DE,HL
	POP	BC
BDOS6:
	POP	AF
	LD	SP,(SAVE_SP)
	RET

CONST1:
	LD	C,0BH
	JP	SYSTEM
CONIN1:
	LD	C,7
	JP	SYSTEM
CONOUT1:
	LD	E,C
	LD	C,2
	JP	SYSTEM

_SYS09:		;_STROUT 文字列出力
	LD	A,(DE)
	INC	DE
	CP	'$'
	JR	Z,S09X1
	PUSH	DE
	LD	C,2
	LD	E,A
	CALL	BDOS
	POP	DE
	JR	_SYS09

S09X1:
	LD	SP,(SAVE_SP)
	LD	IX,(SAVE_IX)
	LD	IY,(SAVE_IY)
	RET

RDABS:
	LD	A,(_DTA+1)
	ADD	A,A
	JP	C,BDOS_EXEC

	PUSH	DE
	PUSH	HL
	LD	DE,BUF512
	LD	C,01AH		;_SETDTA
	CALL	BDOS
	POP	HL
	POP	DE

	LD	BC,(_DTA)
RDABS1:
	PUSH	DE
	PUSH	HL
	PUSH	BC
	LD	H,1
	LD	C,02FH		;_RDABS
	CALL	BDOS
	POP	HL		;<=転送先
	CP	0FFH
	JR	Z,RDABS_ERR
	EX	DE,HL
	LD	HL,BUF512
	LD	BC,512
	LDIR
	LD	C,E
	LD	B,D
	POP	HL
	POP	DE
	INC	DE
	DEC	H
	JR	NZ,RDABS1
WRABS_OK:
	XOR	A
WRABS_ERR:
	PUSH	DE
	PUSH	HL
RDABS_ERR:
	PUSH	AF
	LD	DE,(_DTA)
	LD	C,01AH		;_SETDTA
	CALL	BDOS
	POP	AF
	POP	HL
	POP	DE
	LD	SP,(SAVE_SP)
	RET

WRABS:
	LD	A,(_DTA+1)
	ADD	A,A
	JP	C,BDOS_EXEC
	PUSH	DE
	PUSH	HL
	LD	DE,BUF512
	LD	C,01AH		;_SETDTA
	CALL	BDOS
	POP	HL
	POP	DE

	LD	BC,(_DTA)
WRABS1:
	PUSH	DE
	PUSH	HL
	LD	L,C
	LD	H,B
	LD	DE,BUF512
	LD	BC,512
	LDIR
	EX	(SP),HL
	POP	DE

	PUSH	DE
	PUSH	HL
	LD	H,1
	LD	C,030H		;_WRABS
	CALL	BDOS
	POP	HL
	POP	DE
	POP	BC
	CP	0FFH
	JR	Z,WRABS_ERR
	INC	DE
	DEC	H
	JR	NZ,WRABS1
	JR	WRABS_OK

SAVE_A:
	DB	0
DE_MEM_SIZE:
	DW	0
EXTSP:
	DW	0
DEFAB:
	DW	0
_DTA:
	DW	0
SAVE_C:
	DB	0
IS_SAVE_IX_VAUE:
	DB	0
SAVE_IX:
	DW	0
SAVE_IY:
	DW	0
SAVE_DE:
	DW	0
SAVE_HL:
	DW	0
SAVE_SP:
	DW	0
MAJOR_VER:
	DB	0
