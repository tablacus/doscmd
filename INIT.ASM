;
;	Tablacus DOS cmd
;
	LD	C,06FH		;(BDOS)MSX-DOS のバージョン番号の獲得
	CALL	BDOS
	LD	A,B
	LD	(MAJOR_VER),A
	LD	HL,08A91H
	SBC	HL,DE
	JR	NZ,NOTABLACUS
	LD	A,1
	LD	(MAJOR_VER),A
NOTABLACUS:
					;BASICのフリーエリア位置をずらす
	LD	HL,BASIC_START1
	OR	A
	JR	Z,DOS1START
	LD	HL,BASIC_START2
DOS1START:
	LD	(TXTTAB),HL
	XOR	A
	LD	(HL),A
	DEC	HL
	LD	(HL),A
	LD	HL,0
	LD	(BASIC_DEF),HL

	DI
	LD	A,(RAMAD0)
	CALL	ENASLT_00H

	DB	03EH	;LD A,?
	POP	AF
	LD	(H_CMD),A

	LD	A,0C3H		;JP

	LD	HL,CPM_WBOOT
	LD	(00000H),A
	LD	(00001H),HL

	LD	HL,BDOS0
	LD	(00005H),A
	LD	(00006H),HL

	LD	HL,BDOS1
	LD	(BDOS0),A
	LD	(BDOS0+1),HL

	LD	(H_CMD+1),A
	LD	HL,_CMD
	LD	(H_CMD+2),HL

	LD	A,(MAJOR_VER)
	OR	A
	JR	NZ,ISC_END
				;インタースロットコール
	LD	A,0C3H		;JP
	LD	HL,RDSLT
	LD	(_RDSLT),A
	LD	(_RDSLT+1),HL

	LD	HL,WRSLT
	LD	(_WRSLT),A
	LD	(_WRSLT+1),HL

	LD	HL,CALSLT
	LD	(_CALSLT),A
	LD	(_CALSLT+1),HL

	LD	HL,ENASLT
	LD	(_ENASLT),A
	LD	(_ENASLT+1),HL

	LD	HL,CALLF
	LD	(_CALLF),A
	LD	(_CALLF+1),HL
				;割り込み
	LD	HL,INT38H
	LD	(00038H),A
	LD	(00038H+1),HL

	LD	HL,AT_3B
	LD	DE,ENASUB
	LD	BC,AT_3B_E-AT_3B
	LDIR
ISC_END:
	LD	A,(EXPTBL)
	CALL	ENASLT_00H
	EI

	LD	DE,TITLE
	LD	C,9
	CALL	BDOS
	LD	A,(MAJOR_VER)
	OR	A
	JR	Z,MSX1
	DEC	A
	JR	Z,TABLACUS
	LD	E,'2'
	LD	C,2
	CALL	BDOS

	LD	HL,FILE_DOS2
	LD	DE,FILE
	LD	BC,FILE_DOS2_E-FILE_DOS2
	LDIR

	LD	HL,READ_DOS2
	LD	DE,READ_DOS1
	LD	BC,READ_DOS2_E-READ_DOS2
	LDIR

	LD	A,0CDH		;CALL
	LD	(PARAM_SWC),A
	LD	HL,PARAM_DOS2-FILE_DOS2+FILE
	LD	(PARAM_SWC+1),HL

	JR	MSX1
TABLACUS:
	LD	DE,TITLET
	LD	C,9
	CALL	BDOS

	LD	HL,FILE_TDRL
	LD	DE,FILE
	LD	BC,FILE_TDRL_E-FILE_TDRL
	LDIR
MSX1:
	LD	DE,TITLE2
	LD	C,9
	CALL	BDOS
	RET

; Version
TITLE:
	DB	"Tablacus DOS$"
TITLE2:
	DB	" cmd v0.10"
	DB	00DH,00AH,'$'
TITLET:
	DB	"(ROM)$"

FILE_DOS2:				;DOS2
	CALL	SPCUT
	PUSH	DE
	LD	C,05BH		;(BDOS) パス名の解析
	CALL	BDOS
	POP	DE
	INC	DE
	LD	A,(DE)
	CP	':'
	JR	NZ,FILED2_1
	LD	A,C
	LD	(FDRV),A
FILED2_1:
	EX	DE,HL
	LD	HL,FNAME
	LD	C,05CH		;(BDOS) ファイル名の解析
	JP	BDOS

PARAM_DOS2:
	LD	HL,N_PARAMETERS-FILE_DOS2+FILE
	LD	DE,DTA1+1
	LD	C,06CH		;_SENV 環境変数の設定
	CALL	BDOS
	XOR	A
	LD	L,A
	LD	H,A
	RET
N_PROGRAM:
	DB	"PROGRAM",0
N_PARAMETERS:
	DB	"PARAMETERS",0
COM_EXT:
	DB	".COM",0
FILE_DOS2_E:

FILE_TDRL:			;Tablacus DISK ROM Lite
	LD	HL,FDRV
	LD	C,029H		;(BDOS) パス名の解析
	JP	BDOS
FILE_TDRL_E:

READ_DOS2:
	PUSH	DE
	LD	A,(DE)
	PUSH	AF
	EX	DE,HL
	LD	DE,BUFSTR
	XOR	A
	SBC	HL,DE
	LD	C,L
	LD	B,H
	LD	HL,BUFSTR
	LD	DE,COMSTR
	LDIR
	LD	HL,COM_EXT
	LD	BC,5
	LDIR
	LD	HL,N_PROGRAM-FILE_DOS2+FILE
	LD	DE,COMSTR
	LD	C,06CH		;_SENV 環境変数の設定
	CALL	BDOS
	LD	DE,COMSTR
	LD	C,043H		;(BDOS)ファイルハンドルのオープン
	CALL	BDOS
	POP	HL
	OR	A
	POP	DE
	JP	NZ,WBOOT1
	LD	A,H
	LD	(DE),A
	PUSH	DE
	PUSH	BC
	LD	DE,00100H
	LD	HL,BDOS0-00100H
	LD	C,048H		;(BDOS)ファイルハンドルからの読み出し
	CALL	BDOS
	POP	BC
	LD	C,045H		;(BDOS)ファイルハンドルのクローズ
	CALL	BDOS
	POP	DE
	RET
READ_DOS2_E:
